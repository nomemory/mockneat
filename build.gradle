plugins {
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

group = "net.andreinc"
version = "0.4.2"
archivesBaseName = "mockneat"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {

    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
    compile group: 'org.apache.commons', name: 'commons-text', version: '1.9'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.15'
    compile group: 'com.github.mifmif', name: 'generex', version: '1.0.2'
    compile 'net.andreinc.aleph:aleph-formatter:0.1.1'
    compile 'net.andreinc.markovneat:markovneat:1.8'

    testCompile group: 'com.opencsv', name: 'opencsv', version: '5.3'
    testCompile group: 'junit', name: 'junit', version: '4.13.1'
    testCompile group: 'commons-validator', name: 'commons-validator', version: '1.7'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'mockneat library'
                packaging 'jar'
                description "Mockneat data generator library"
                url 'http://www.mockneat.com'

                scm {
                    connection 'https://github.com/nomemory/mockneat'
                    developerConnection 'https://github.com/nomemory/mockneat'
                    url 'https://github.com/nomemory/mockneat'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'nomemory'
                        name 'nomemory'
                        email 'gnomemory@yahoo.com'
                    }
                }
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport
