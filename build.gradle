plugins {
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

group = "net.andreinc"
version = "0.4.5"
archivesBaseName = "mockneat"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {

    compile 'org.apache.commons:commons-lang3:3.11'
    compile 'org.apache.commons:commons-text:1.9'
    compile 'commons-codec:commons-codec:1.15'
    compile 'com.github.mifmif:generex:1.0.2'
    compile 'net.andreinc:aleph:0.1.1'
    compile 'net.andreinc:markovneat:1.8'

    testImplementation group: 'com.opencsv', name: 'opencsv', version: '5.3'
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'commons-validator', name: 'commons-validator', version: '1.7'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'mockneat library'
                packaging 'jar'
                description "Mockneat data generator library"
                url 'http://www.mockneat.com'

                scm {
                    connection 'https://github.com/nomemory/mockneat'
                    developerConnection 'https://github.com/nomemory/mockneat'
                    url 'https://github.com/nomemory/mockneat'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'nomemory'
                        name 'nomemory'
                        email 'gnomemory@yahoo.com'
                    }
                }
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport
